apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'nexus'

sourceCompatibility     = 1.8
targetCompatibility     = 1.8
version                 = '0.1.0-SNAPSHOT'
group                   = "fr.cea.ig.microscope.client.model"
def projectName         = 'microscope-client-model'
def projectDescription  = 'Microscope object used while query microscope remote api'
def projectUrl          = 'https://github.com/institut-de-genomique/microscope-model-client'


task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}


dependencies {
    compile     group: 'org.springframework',               name: 'spring-web',                 version: '4.0.6.RELEASE'
    compile     group: 'com.fasterxml.jackson.core',        name: 'jackson-databind',           version: '2.4.1.3'
}

artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

jar {
    manifest.attributes provider: 'genoscope'
    manifest.attributes 'Created-By': 'LABGeM'
    manifest.attributes 'Product-Name': projectName
    baseName    = projectName
    version     = version
}


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://gsphere.genoscope.cns.fr/nexus/content/groups/public'
    }
}


buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

modifyPom {
    project {
        name            projectName
        description     projectDescription
        url             projectUrl
        inceptionYear   '2014'

        scm {
            url                 'https://github.com/institut-de-genomique/microscope-model-client'
            connection          'scm:https://github.com/institut-de-genomique/microscope-model-client.git'
            developerConnection 'scm:git@github.com:institut-de-genomique/microscope-model-client.git'
        }

        licenses {
            license {
                name            'CeCILL'
                url             'http://www.cecill.info/licences/Licence_CeCILL_V2.1-en.html'
                distribution    'repo'
            }
        }

        developers {
            developer {
                id              'jmercier'
                name            'Jonathan MERCIER'
                email           'jmercier@genoscope.cns.fr'
                timezone        '+1'
            }
        }
    }
}

nexus {
    attachSources           = false
    attachTests             = true
    attachJavadoc           = false
    sign                    = false
    repositoryUrl           = 'https://gsphere.genoscope.cns.fr/nexus/content/repositories/releases/'
    snapshotRepositoryUrl   = 'https://gsphere.genoscope.cns.fr/nexus/content/repositories/snapshots/'
}
